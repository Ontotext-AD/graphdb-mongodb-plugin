PREFIX bbcevent:<http://www.bbc.co.uk/ontologies/event/> 
PREFIX geo-pos:<http://www.w3.org/2003/01/geo/wgs84_pos#> 
PREFIX bbc:<http://www.bbc.co.uk/ontologies/bbc/> 
PREFIX time:<http://www.w3.org/2006/time#> 
PREFIX event:<http://purl.org/NET/c4dm/event.owl#> 
PREFIX music-ont:<http://purl.org/ontology/mo/> 
PREFIX rdf:<http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX foaf:<http://xmlns.com/foaf/0.1/> 
PREFIX provenance:<http://www.bbc.co.uk/ontologies/provenance/> 
PREFIX owl:<http://www.w3.org/2002/07/owl#> 
PREFIX cms:<http://www.bbc.co.uk/ontologies/cms/> 
PREFIX news:<http://www.bbc.co.uk/ontologies/news/> 
PREFIX cnews:<http://www.bbc.co.uk/ontologies/news/cnews/> 
PREFIX cconcepts:<http://www.bbc.co.uk/ontologies/coreconcepts/> 
PREFIX dbp-prop:<http://dbpedia.org/property/> 
PREFIX geonames:<http://sws.geonames.org/> 
PREFIX rdfs:<http://www.w3.org/2000/01/rdf-schema#> 
PREFIX domain:<http://www.bbc.co.uk/ontologies/domain/> 
PREFIX dbpedia:<http://dbpedia.org/resource/> 
PREFIX geo-ont:<http://www.geonames.org/ontology#> 
PREFIX bbc-pont:<http://purl.org/ontology/po/> 
PREFIX tagging:<http://www.bbc.co.uk/ontologies/tagging/> 
PREFIX sport:<http://www.bbc.co.uk/ontologies/sport/> 
PREFIX skosCore:<http://www.w3.org/2004/02/skos/core#> 
PREFIX dbp-ont:<http://dbpedia.org/ontology/> 
PREFIX xsd:<http://www.w3.org/2001/XMLSchema#> 
PREFIX core:<http://www.bbc.co.uk/ontologies/coreconcepts/> 
PREFIX curric:<http://www.bbc.co.uk/ontologies/curriculum/> 
PREFIX skos:<http://www.w3.org/2004/02/skos/core#> 
PREFIX cwork:<http://www.bbc.co.uk/ontologies/creativework/> 
PREFIX fb:<http://rdf.freebase.com/ns/>
PREFIX ot:<http://www.ontotext.com/> 
PREFIX ldbcspb:<http://www.ldbcouncil.org/spb#>
PREFIX : <http://www.ontotext.com/connectors/mongodb#>
PREFIX inst: <http://www.ontotext.com/connectors/mongodb/instance#>

#  Query Name : query12
#  Query Description : 
#  Retrieve the descriptions of the latest creative works tagged with a specific location. Consider that the description of each specific 
#  Creative Work is stored in dedicated named graph. The result should include only the explicit statements about the creative work, without owl:sameAs 
#  equivalence and without statements inferred otherwise .

SELECT ?cworkGraph ?s ?p ?o
#FROM ot:disable-sameAs
WHERE {
    {
        {
            select (group_concat(?tagStr;
                    separator = '", "') as ?arg1)
            {
                values ?tag {
                    "http://dbpedia.org/resource/AccessAir"
                    "http://dbpedia.org/resource/Battle_of_Bristoe_Station"
                }
                bind (replace(str(?tag), "http://sws.geonames.org/", "geonames:") as ?tmp1)
                bind (replace(str(?tmp1), "http://dbpedia.org/resource/", "dbpedia:") as ?tagStr)
            }
        }
        values ?template {
            '''[
{
"$match": {
"$or": [{"@graph.cwork:about.@id": {"$in": ["$arg1"]}}, {"@graph.cwork:mentions.@id": {"$in": ["$arg1"]}}]
}},
{"$sort": {"@graph.cwork:dateModified.@date": -1}},
{"$limit": 5},
{'$addFields': {'@graph.cwork:graph.@id' :  '$@id'}}
]'''
        }
        bind (replace(str(?template), "\\$arg1", ?arg1) as ?query )
        {
            ?search a inst:spb100 ;
                    :aggregate ?query ;
                    :entity ?entity .
            graph inst:spb100 {
                ?creativeWork cwork:graph ?cworkGraph .
                ?s ?p ?o .
            }
        }
    }
}
ORDER BY ?cworkGraph ?s ?p ?o